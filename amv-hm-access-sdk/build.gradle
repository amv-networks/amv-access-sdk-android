
apply plugin: 'com.github.dcendents.android-maven' // needed for jitpack

group = 'com.github.amv-networks.amv-access-sdk-android'

apply plugin: 'com.android.library'

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        mavenLocal()
        maven {
            url "https://dl.bintray.com/amvnetworks/amv-access-api-poc"
        }
        flatDir {
            dirs 'libs'
        }
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        destinationDir = file("../docs/${project.name}/javadoc")
        exclude 'lombok.config'
        failOnError = false
        options.noTimestamp = true
        options.locale 'en_US'
    }
}

dependencies {
    compileOnly(name: 'hmkit-android', version: "${hmkitVersion}", ext: 'aar')

    // currently pulled in because it is needed by hmkit (otherwise NoClassDefFoundErrors are thrown)
    // this can be removed once the webservice layer has been externalized by hm
    implementation 'com.android.volley:volley:1.1.0'

    api project(':amv-access-sdk-spi')
    implementation "org.amv.access:client-android:${amvAccessClientVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.facebook.conceal:conceal:${concealVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.apache.commons:commons-lang3:${commonsLangVersion}"

    androidTestImplementation "com.android.support.test:runner:${supportTestVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoVersion}"

    // jar files are explicitly pulled in because 'hmkit-android' does not have a groupId
    // which leads to issues when downloading the lib from jcenter/central/etc.
    // ("Could not parse POM: Missing required attribute: dependency groupId")
    // fails -> testImplementation(name: 'hmkit-android', version: "${hmkitVersion}", ext: 'aar')
    testImplementation fileTree(include: ['*.jar'], dir: 'libs')
}
